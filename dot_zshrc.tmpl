# Exit if not running interactively (but allow for testing)
[[ $- != *i* ]] && [[ -z "$ZSH_TEST_MODE" ]] && return

# Profile checkpoint helper
[[ -n "$ZSH_PROFILE_STARTUP" ]] && typeset -f _zsh_profile_checkpoint >/dev/null && _zsh_profile_checkpoint "zshrc start"

# Disable terminal bell (prevents tmux ! indicator and annoying beeps)
unsetopt BEEP

# Disable Oh My Zsh automatic updates and skip compfix check for speed
export DISABLE_AUTO_UPDATE=true
export ZSH_DISABLE_COMPFIX=true

# NOTE: PATH is managed in ~/.config/shell/path.sh (loaded at the end of this file)
# Only ~/.local/bin is set in ~/.zshenv for non-interactive shells

# Path to your Oh My Zsh installation
export ZSH="$HOME/.oh-my-zsh"

# Theme - Disabled (using Starship prompt instead)
ZSH_THEME=""

# Load zsh fixes before Oh My Zsh initialization
[[ -f "$HOME/.config/shell/zsh-fix.sh" ]] && source "$HOME/.config/shell/zsh-fix.sh"

[[ -n "$ZSH_PROFILE_STARTUP" ]] && typeset -f _zsh_profile_checkpoint >/dev/null && _zsh_profile_checkpoint "zsh-fix loaded"

# Configure oh-my-zsh plugins
zstyle :omz:plugins:ssh-agent identities id_ed25519
zstyle :omz:plugins:ssh-agent lifetime 1h
zstyle :omz:plugins:ssh-agent quiet yes
zstyle :omz:plugins:jj ignore-working-copy yes

plugins=(
	git
	jj
	terraform
	ansible
	ssh-agent
	tmux
	zsh-autosuggestions
	zsh-syntax-highlighting
)

# Only proceed with Oh My Zsh configuration if it's installed
if [[ -d "$ZSH" ]]; then
	# IFS is already properly set by zsh-fix.sh

	# Load Oh My Zsh
	source "$ZSH/oh-my-zsh.sh"

	[[ -n "$ZSH_PROFILE_STARTUP" ]] && typeset -f _zsh_profile_checkpoint >/dev/null && _zsh_profile_checkpoint "oh-my-zsh loaded"

	# Restore IFS if needed (set by zsh-fix.sh)
	if [[ -n "$_ifs_restore_needed" ]] && declare -f _restore_ifs >/dev/null; then
		_restore_ifs
		unset _ifs_restore_needed
	fi

else
	echo "Warning: Oh My Zsh not found at $ZSH"
fi

# SSH host detection for Starship prompt (generic hostname for privacy)
if [[ -n "$SSH_CLIENT" ]] || [[ -n "$SSH_TTY" ]] || [[ -n "$SSH_CONNECTION" ]]; then
    _starship_host_hash=$(echo "$HOSTNAME" | (md5sum 2>/dev/null || md5 2>/dev/null || echo "1") | cut -c1-1)
    case $_starship_host_hash in
        [0-3]) export STARSHIP_SSH_HOST="server1" ;;
        [4-7]) export STARSHIP_SSH_HOST="server2" ;;
        [8-b]) export STARSHIP_SSH_HOST="server3" ;;
        *) export STARSHIP_SSH_HOST="server4" ;;
    esac
    unset _starship_host_hash
    unset STARSHIP_LOCAL_HOST
else
    export STARSHIP_LOCAL_HOST="localhost"
    unset STARSHIP_SSH_HOST
fi

[[ -n "$ZSH_PROFILE_STARTUP" ]] && typeset -f _zsh_profile_checkpoint >/dev/null && _zsh_profile_checkpoint "host detection done"

# Enable vi mode keybindings (only if ZLE is available)
if [[ -n "${ZLE_VERSION:-}" ]] || zmodload zsh/zle 2>/dev/null; then
	bindkey -v
fi

# ZLE error fixes are handled by zsh-fix.sh loaded earlier

# OCaml/opam configuration
# This loads opam configuration and environment variables
[[ ! -r "$HOME/.opam/opam-init/init.zsh" ]] || source "$HOME/.opam/opam-init/init.zsh" > /dev/null 2> /dev/null

# NVM is lazy-loaded in ~/.zshenv â€” completion loads on first use of nvm/node/npm/npx

[[ -n "$ZSH_PROFILE_STARTUP" ]] && typeset -f _zsh_profile_checkpoint >/dev/null && _zsh_profile_checkpoint "nvm (lazy, skipped)"

# Load additional shell configurations from your config directory.
for conf in "$HOME/.config/shell/"*.sh; do
	[ -f "$conf" ] && . "$conf"
done

[[ -n "$ZSH_PROFILE_STARTUP" ]] && typeset -f _zsh_profile_checkpoint >/dev/null && _zsh_profile_checkpoint "shell configs loaded"

# Initialize Starship prompt
if command -v starship >/dev/null 2>&1; then
    eval "$(starship init zsh)"
fi

[[ -n "$ZSH_PROFILE_STARTUP" ]] && typeset -f _zsh_profile_checkpoint >/dev/null && _zsh_profile_checkpoint "starship init"

# Auto-start tmux (placed at the end to ensure all environment is set up first)
# Skipped for: SSH sessions, VSCode terminals, already in tmux, or NOTMUX=1
if [[ -z "$TMUX" ]] && [[ -z "$SSH_TTY" ]] && [[ "$TERM_PROGRAM" != "vscode" ]] && [[ -z "$NOTMUX" ]]; then
    # Attempt to attach to existing session or create a new one
    exec tmux new-session -A
fi

# bun completions (bun PATH is set in ~/.zshenv)
[ -s "$HOME/.bun/_bun" ] && source "$HOME/.bun/_bun"

# uv completions (uv is set up in ~/.zshenv) - cached for performance
if command -v uv >/dev/null 2>&1; then
    local uv_comp="$HOME/.cache/uv-completion.zsh"
    if [[ ! -f "$uv_comp" ]] || [[ "$(command -v uv)" -nt "$uv_comp" ]]; then
        mkdir -p "$HOME/.cache"
        uv generate-shell-completion zsh > "$uv_comp"
    fi
    source "$uv_comp"
fi

# Final profiling checkpoint and display report
if [[ -n "$ZSH_PROFILE_STARTUP" ]]; then
    _zsh_profile_end=$(python3 -c 'import time; print(int(time.time()*1000))' 2>/dev/null || echo 0)

    # Auto-display report if function is available
    if typeset -f zsh_profile_report >/dev/null; then
        zsh_profile_report
    fi
fi
